
&НаКлиенте
Асинх Процедура Загрузка(Команда)
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = "Текстовый документ|*.txt";
	
	Результат = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузкаНаСервере(Результат.Адрес);

КонецПроцедуры

&НаСервере
Процедура ЗагрузкаНаСервере(АдресВременногоХранилища)
	
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	ДанныеДляОбмена.Прочитать(ДанныеФайла.ОткрытьПотокДляЧтения(), КодировкаТекста.UTF8);
	
	КоличествоСтрок = ДанныеДляОбмена.КоличествоСтрок();
	
	РежимРаботы = "ПоискОбъекта";
	
	Для Сч = 1 По КоличествоСтрок Цикл
		
		Строка = ДанныеДляОбмена.ПолучитьСтроку(Сч);
		
		Если РежимРаботы = "ПоискОбъекта" Тогда
			Если Строка = "Начало: ТипНомера" Тогда
				МенеджерОбъекта = Справочники.ТипыНомеров;
			ИначеЕсли Строка = "Начало: Бронирование" Тогда 
				МенеджерОбъекта = Документы.Бронирование;
			Иначе
				ВызватьИсключение "Некорректный формат файла выгрузки";
			КонецЕсли;
			РежимРаботы = "ЧтениеРеквизитов";
			НомерРеквизита = 0;
			Продолжить;
		КонецЕсли;
		
		Если НомерРеквизита = 0 Тогда
			Ссылка = МенеджерОбъекта.ПолучитьСсылку(Новый УникальныйИдентификатор(Строка));
			ЗагружаемыйОбъект = Ссылка.ПолучитьОбъект();
			Если ЗагружаемыйОбъект = Неопределено Тогда
				Если ТипЗнч(МенеджерОбъекта) = Тип("ДокументМенеджер.Бронирование") Тогда
					ЗагружаемыйОбъект = МенеджерОбъекта.СоздатьДокумент();
					ЗагружаемыйОбъект.Дата = ТекущаяДатаСеанса();
				ИначеЕсли ТипЗнч(МенеджерОбъекта) = Тип("СправочникМенеджер.ТипыНомеров") Тогда
					ЗагружаемыйОбъект = МенеджерОбъекта.СоздатьЭлемент();
				КонецЕсли;
				ЗагружаемыйОбъект.УстановитьСсылкуНового(Ссылка);
			КонецЕсли;
		ИначеЕсли СтрНачинаетсяС(Строка, "Конец: ") Тогда
			ЗагружаемыйОбъект.ОбменДанными.Загрузка = Истина;
			ЗагружаемыйОбъект.Записать();
			РежимРаботы = "ПоискОбъекта";
		Иначе
			ЗаполнитьЗначениеРеквизитаОбъект(НомерРеквизита, ЗагружаемыйОбъект, Строка);
		КонецЕсли;
				
		НомерРеквизита = НомерРеквизита + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначениеРеквизитаОбъект(НомерРеквизита, ЗагружаемыйОбъект, Строка)
	
	Если ТипЗнч(ЗагружаемыйОбъект) = Тип("ДокументОбъект.Бронирование") Тогда
		Если НомерРеквизита = 1 Тогда
			ЗагружаемыйОбъект.ДатаЗаезда = ПрочитатьДатуJSON(Строка, ФорматДатыJSON.ISO);
		ИначеЕсли НомерРеквизита = 2 Тогда 
			ЗагружаемыйОбъект.ДатаВыезда = ПрочитатьДатуJSON(Строка, ФорматДатыJSON.ISO);
		ИначеЕсли НомерРеквизита = 3 Тогда 
			ЗагружаемыйОбъект.ТипНомера = 
				Справочники.ТипыНомеров.ПолучитьСсылку(Новый УникальныйИдентификатор(Строка));
		КонецЕсли;			
	ИначеЕсли ТипЗнч(ЗагружаемыйОбъект) = Тип("СправочникОбъект.ТипыНомеров") Тогда
		Если НомерРеквизита = 1 Тогда
			ЗагружаемыйОбъект.Наименование = Строка;
		ИначеЕсли НомерРеквизита = 2 Тогда 
			ЗагружаемыйОбъект.Количество = Строка;
		ИначеЕсли НомерРеквизита = 3 Тогда 
			ЗагружаемыйОбъект.Цена = Строка;
		ИначеЕсли НомерРеквизита = 4 Тогда 
			ЗагружаемыйОбъект.Описание = СтрЗаменить(Строка, "\n", Символы.ПС);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузка(Команда)
	
	Адрес = ВыгрузкаНаСервере();
	
	ПараметрыПолучения = Новый ПараметрыДиалогаПолученияФайлов;
	ПолучитьФайлССервераАсинх(Адрес, "exchange.txt", ПараметрыПолучения);

КонецПроцедуры

&НаСервере
Функция ВыгрузкаНаСервере()
	
	ДанныеДляОбмена.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТипыНомеров.Ссылка,
		|	ТипыНомеров.Наименование,
		|	ТипыНомеров.Количество,
		|	ТипыНомеров.Цена,
		|	ТипыНомеров.Описание
		|ИЗ
		|	Справочник.ТипыНомеров КАК ТипыНомеров";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеДляОбмена.ДобавитьСтроку("Начало: ТипНомера");
		
		ДанныеДляОбмена.ДобавитьСтроку(Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ДанныеДляОбмена.ДобавитьСтроку(Выборка.Наименование);
		ДанныеДляОбмена.ДобавитьСтроку(XMLСтрока(Выборка.Количество));
		ДанныеДляОбмена.ДобавитьСтроку(XMLСтрока(Выборка.Цена));
		ДанныеДляОбмена.ДобавитьСтроку(СтрЗаменить(Выборка.Описание, Символы.ПС, "\n"));
		
		ДанныеДляОбмена.ДобавитьСтроку("Конец: ТипНомера");
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Бронирование.Ссылка,
		|	Бронирование.ДатаЗаезда,
		|	Бронирование.ДатаВыезда,
		|	Бронирование.ТипНомера
		|ИЗ
		|	Документ.Бронирование КАК Бронирование";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеДляОбмена.ДобавитьСтроку("Начало: Бронирование");
		
		ДанныеДляОбмена.ДобавитьСтроку(Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ДанныеДляОбмена.ДобавитьСтроку(ЗаписатьДатуJSON(Выборка.ДатаЗаезда, ФорматДатыJSON.ISO));
		ДанныеДляОбмена.ДобавитьСтроку(ЗаписатьДатуJSON(Выборка.ДатаВыезда, ФорматДатыJSON.ISO));
		ДанныеДляОбмена.ДобавитьСтроку(Строка(Выборка.ТипНомера.УникальныйИдентификатор()));
		
		ДанныеДляОбмена.ДобавитьСтроку("Конец: Бронирование");
		
	КонецЦикла;
	
	Поток = Новый ПотокВПамяти();
	
	ДанныеДляОбмена.Записать(Поток);
	
	ДанныеФайла = Поток.ЗакрытьИПолучитьДвоичныеДанные();
	
	Адрес = ПоместитьВоВременноеХранилище(ДанныеФайла);
	
	Возврат Адрес;
	
КонецФункции
